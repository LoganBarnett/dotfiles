#+title:    evil-mode
#+author:   Logan Barnett
#+email:    logustus@gmail.com
#+date:     <2019-01-02 Wed>
#+language: en
#+tags:     evil-mode emacs config

* commenting

=g c= is the default comment operator. Spacemacs had =g y= for command + yank,
but Doom did not persist that. I think that's because Doom uses =evilnc= rather
than =evil-commentary=. Instead, Doom reserves =g y= for yank-unindented, which
does have its uses.

=evilnc-yank-and-comment-operator= covers this functionality.

Let's bind it to =g c=. I have tried things like =g C= and even =g Y= (although
it's taken already) but it's really weird since =evil-mode= is expecting me to
use =c= or =C= again in the case of =g C=, and I think it messes up the
operator. Instead, I don't think there's a time when I comment and I don't want
it to enter the kill ring.

The author of =evilnc= was very kind and quick to add
=evilnc-yank-and-comment-operator= for me. However it seemed to be buggy when I
tried it in =rust-mode=. The implementation below suits my needs better,
although it has a problem with doubling up on indentation, at least when
pasting. I'm not sure what to do about that. Perhaps I have to use the copy-line
equivalent function when that motion is provided. This bug only effects the
first line.

#+name: config/evil-comment-yanks
#+begin_src emacs-lisp :results none :tangle yes

(evil-define-operator custom/evilnc-yank-and-comment-operator (begin end type register)
  "(Un)comment and yank the text from BEGIN to END."
  :move-point nil
  :repeat nil
  (interactive "<r><x><y>")
  (evil-yank-line begin end type register)
  (evilnc-comment-or-uncomment-region begin end)
  )

(map!
  :nv "g c" #'custom/evilnc-yank-and-comment-operator
  )
#+end_src


* evil-mode
  #+name: config/evil-Y-yanks-to-eol
  #+begin_src emacs-lisp :tangle yes
    ;; (message "nothing to do in evil-mode config")
    (setq-default evil-want-Y-yank-to-eol t)
  #+end_src
** evil-collection
   =evil-collection= is a user maintained list of =evil-mode= bindings. It's not
   very compatible with the Spacemacs way of doing things, and I've had trouble
   getting some of the keybindings to work properly. For now I've opted to not
   use =evil-collection= and instead wire up my own bindings by hand. I've found
   a way to do this that seems to work consistently enough.
* evil-snipe
The =evil-snipe= package promises better/smarter searching for =f=, =F=, =t=, and
=T=.
** case sensitivity
Doom uses the smart case by default. While I enjoy this with =/= I do not enjoy
it here. The motions =evil-snipe= uses need to be precise in my mind.

#+name: config/evil-snipe-smart-case-disable
#+begin_src emacs-lisp :results none :tangle yes
(setq-default evil-snipe-smart-case nil)
#+end_src
** overriding normal seek keys

=evil-snipe= will override =f/F/t/T= when sniping. I find this confusing and
counter to my vanilla vim bindings. Disable it. We can use =;= and =,= for that.

#+name: config/evil-snipe-override-disable
#+begin_src emacs-lisp :results none :tangle yes
(setq-default
 evil-snipe-override-evil-repeat-keys t
 evil-snipe-repeat-keys nil
 )

;; I couldn't get map! to do what I wanted here. Left for reference.
;; `evil-global-set-key' seems to do the trick though.
;;
;; (map!
;;  :mnv :desc "evil-snipe search repeat" ";" #'evil-snipe-repeat
;;  :mnv :desc "evil-snipe search repeat reverse" "," #'evil-snipe-repeat-reverse
;;  )
;;
;; TODO: I should probably file a pull request or something here. Without this,
;; ; and , are completely broken. This is because when not in the transient map,
;; ; and , do the normal evil repeat search, which doesn't work since evil-snipe
;; tracks what has been searched. "No previous search" is displayed, or
;; something like that.
(evil-global-set-key 'motion (kbd ";") #'evil-snipe-repeat)
(evil-global-set-key 'motion (kbd ",") #'evil-snipe-repeat-reverse)
#+end_src
** fix whitespace seeking motion

=evil-snipe= has a behavior, that when switched on, won't consume or include
whitespace when seeking to whitespace. For example, use =d f SPC= or =v f SPC=
before two words. This is recorded in [[https://github.com/hlissner/evil-snipe/issues/70][evil-snipe#70]]. This can be addressed by
setting =evil-snipe-skip-leading-whitespace= to =nil=.

#+name: config/evil-snipe-fix-whitespace-seek
#+begin_src emacs-lisp :results none :tangle yes
(setq-default evil-snipe-skip-leading-whitespace nil)
#+end_src


** apply

We're getting desperate.

#+begin_src emacs-lisp :results none :noweb yes
(use-package "evil-snipe"
  :init
  <<config/evil-snipe-smart-case-disable>>
  <<config/evil-snipe-override-disable>>
  <<config/evil-snipe-fix-whitespace-seek>>
  :config
  <<config/evil-snipe-smart-case-disable>>
  )
#+end_src
* evil-iedit-state
The [[https://github.com/syl20bnr/evil-iedit-state][evil-iedit-state]] package is something I encountered first with Spacemacs. It
brings up a [[https://github.com/abo-abo/hydra][hydra]] interface when pressing =*= or =#=. In vim these typically
select all occurrences of the word that the cursor is on, and will do a forward
or reverse search, depending on which of the two were pressed.

=evil-iedit-state= takes this a bit further, and provides a =hydra= state that
allows for interactive editing, which can include refactoring identifiers.

# Disabled for now.
#+begin_src emacs-lisp :results none :tangle no

(defhydra hydra-evil-iedit-state
  (
   ;; None of the keys in the string are actually different from
   ;; evil-iedit-state. Just pass through all key bindings to evil-iedit-state.
   :foreign-keys run
   :hint none
   )
  ;; Yanked from spacemacs-visual-editing.
  "
 _n_ next                _r_ change range         _R_ reset       _e_ iedit
 _N_ previous            _o_ open below
 _d_ next definition     _O_ open above
 _D_ previous definition _F_ restrict to function
 _C-g_/_ESC_ quit
"
  ("n" (iedit-next-occurrence))
  ("N" (iedit-prev-occurrence))
  ("F" (iedit-restrict-function))
  ("o" (evil-iedit-state/evil-open-below))
  ("O" (evil-iedit-state/evil-open-above))

  ("C-g" (evil-iedit-state/quit-iedit-mode) :exit t)
  ("ESC" (evil-iedit-state/quit-iedit-mode) :exit t)
  )

(require 'evil-iedit-state)
(defun config/begin-evil-iedit-state ()
  (interactive) ;; Necessary to satisfy commandp.
  (evil-iedit-state/iedit-mode)
  (hydra-evil-iedit-state/body)
  )
(add-hook 'hydra-evil-iedit-state-exit-hook #'hydra-disable)

(define-key evil-normal-state-map (kbd "*") #'config/begin-evil-iedit-state)
(define-key evil-normal-state-map (kbd "#") #'config/begin-evil-iedit-state)

#+end_src

* searching

Search was broken for a bit, but now it's not...?

#+begin_src emacs-lisp :results none :tangle yes
;; (setq-default evil-search-module 'evil-search)
;;(setq-default org-fold-core-style 'text-properties)
#+end_src

* search and replace
** global search and replace

I found it quite handy to have things like =:%s= and =:s= replace globally by
default, with the option to deactivate it by adding the =g= flag at the end of
the expression. This got switched off during an upgrade, so now I set it
manually.

#+name: config/evil-global-substitute
#+begin_src emacs-lisp :results none :tangle yes
(setq evil-ex-substitute-global t)
#+end_src

* stitch

It may not be apparent but =use-package= works as you'd want it to even if the
library is already loaded.

#+begin_src emacs-lisp :results none :noweb yes
(use-package "evil"
  :init
  <<config/evil-global-substitute>>
  <<config/evil-Y-yanks-to-eol>>
  :config
  <<config/evil-comment-yanks>>
  )

#+end_src
